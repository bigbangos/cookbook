---
title: Strings
---

A collection of utilities for working with move string primitive.

## Convert u64 to String
Simple function to convert a u64 integer into its ASCII string equivalent.

```
module cookbook::utility {
    use std::string::{Self, String};
    use std::vector;

	#[test_only]
	use std::debug;

    fun u64_to_string(input: u64): String{
        //create empty vector to hold digit ASCII values
        let result = vector::empty();

        //loop while input value is 10 or higher
        while (input/10 > 0){
            //modulo the input value by 10 to get the last digit
            let digit = input%10;
            //add 48 to the digit value to get ASCII (where ASCII "0" = 48)
            //and push it into the result array
            vector::push_back(&mut result, ((digit+48) as u8));
            //divide input by 10 to get to the next digit then loop
            input = input/10;
        };
        //at this point, input will only be holding one digit remaining
        vector::push_back(&mut result, ((input+48) as u8));
        //resverse the order because we pushed the digits last to first
        vector::reverse(&mut result);
        //convert the u8 array to utf8 string and return
        string::utf8(result)
    }

    #[test]
    fun test_conversion() {
        let myString = u64_to_string(1234567890u64);
        assert!(string::bytes(&myString) == &b"1234567890", 1);
        debug::print(&myString);
    }
}
```

## Convert address to String
Simple function to convert a Move 32-byte address into its ASCII string equivalent.

```
module cookbook::utility {
	
  use std::string;
  use std::vector;
  use std::bcs;

  #[test_only]
  use std::debug;

  const EINVALID_INPUT: u64 = 0;

  public fun addressToString(input: address): string::String{
	let addr_bytes = bcs::to_bytes(&input);
	//let i = 0;
	let result = vector::empty<u8>();
	while (vector::length<u8>(&addr_bytes)>0) {
		let digit_vec = u8toASCIIvec(vector::pop_back(&mut addr_bytes));
		vector::append(&mut result, digit_vec);
	};
	vector::reverse(&mut result);
	string::utf8(result)
  } 

  fun u8toASCIIvec(input: u8): vector<u8> {
    let result = vector::empty<u8>();
	vector::push_back(&mut result, u4toASCIIu8(input % 16));
    vector::push_back(&mut result, u4toASCIIu8(input / 16));
    result
  }

  fun u4toASCIIu8(input: u8): u8 {
    assert!(input<=15, EINVALID_INPUT);
    if (input<=9) (48 + input) // 0 - 9 => ASCII 48 to 57
    else (55 + input) //10 - 15 => ASCII 65 to 70
  }

  #[test]
  public entry fun test_it() {
	let test_addr = @0x1234567890ABCDEF;
	let test_string = addressToString(test_addr);
	assert!(string::bytes(&test_string) == &b"0000000000000000000000000000000000000000000000001234567890ABCDEF", 1);
    debug::print<string::String>(&test_string);
  }

}
```